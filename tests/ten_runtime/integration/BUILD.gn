#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/options.gni")

group("integration") {
  deps = []

  deps += [ "cpp" ]

  if (ten_enable_go_binding) {
    deps += [ "go" ]
  }

  if (ten_enable_python_binding) {
    deps += [ "python" ]
  }

  if (ten_enable_nodejs_binding) {
    deps += [ "nodejs" ]
  }
}

if (ten_runtime_enable_coverage && is_debug && is_linux) {
  action("run_for_coverage") {
    script = "//tools/coverage/run_integration_tests_for_coverage.py"

    _done = "${target_gen_dir}/run_integration_tests_for_coverage.done"

    args = [
      "--root-out",
      rebase_path(root_out_dir),
      "--is-clang",
    ]
    if (is_clang) {
      args += [ "true" ]
    } else {
      args += [ "false" ]
    }

    args += [
      "--done-file",
      rebase_path(_done),
    ]

    deps = [ ":integration" ]

    outputs = [ _done ]
  }
}
