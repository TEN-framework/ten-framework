#
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/feature/ten_package_test.gni")
import("//build/options.gni")

ten_package("default_extension_cpp") {
  package_kind = "extension"
  enable_build = true

  sources = []
  source_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                             [
                               "--dir",
                               rebase_path("src/**/*.h"),
                               "--dir",
                               rebase_path("src/**/*.cc"),
                               "--recursive",
                             ],
                             "json")
  foreach(source_file, source_files) {
    sources += [ source_file.path ]
  }

  include_dirs = [
    "src",
    "include",

    # The build flags for in-app building
    "//ten_packages/system/ten_runtime/include",

    # The build flags for standalone building.
    ".ten/app/ten_packages/system/ten_runtime/include",
  ]

  lib_dirs = [
    # The build flags for in-app building
    "//ten_packages/system/ten_runtime/lib",

    # The build flags for standalone building.
    ".ten/app/ten_packages/system/ten_runtime/lib",
  ]

  if (is_win) {
    libs = [
      "ten_runtime.dll.lib",
      "ten_utils.dll.lib",
    ]
  } else {
    libs = [
      "ten_runtime",
      "ten_utils",
    ]
  }
}

if (ten_enable_standalone_test) {
  # Note: To perform gtest standalone testing, need to first install the
  # googletest system package.
  #
  # ```shell
  # tman install system googletest
  # ```
  ten_package_test("default_extension_cpp_test") {
    package_kind = "extension"

    sources = []
    source_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                               [
                                 "--dir",
                                 rebase_path("tests/**/*.cc"),
                                 "--recursive",
                               ],
                               "json")
    foreach(source_file, source_files) {
      sources += [ source_file.path ]
    }

    include_dirs = [
      ".ten/app/ten_packages/system/googletest",
      ".ten/app/ten_packages/system/googletest/include",
      ".ten/app/ten_packages/system/ten_runtime/include",
    ]

    lib_dirs = [ ".ten/app/ten_packages/system/ten_runtime/lib" ]

    if (is_win) {
      libs = [
        "ten_runtime.dll.lib",
        "ten_utils.dll.lib",
      ]
    } else {
      libs = [
        "ten_runtime",
        "ten_utils",
      ]

      if (is_mac) {
        ldflags = [ "-Wl,-rpath,@loader_path/../.ten/app/ten_packages/system/ten_runtime/lib" ]
      } else if (is_linux) {
        ldflags = [
          "-Wl,-rpath=\$ORIGIN/../.ten/app/ten_packages/system/ten_runtime/lib",
        ]
      }
    }

    public_deps = [ ".ten/app/ten_packages/system/googletest" ]
  }
}
