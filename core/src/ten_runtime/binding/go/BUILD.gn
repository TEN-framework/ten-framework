#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/feature/ten_package.gni")
import("//build/options.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/feature/ten_go_lint.gni")
import("//build/ten_runtime/options.gni")
import("//core/src/ten_runtime/output_libs.gni")

if (ten_enable_go_binding) {
  if (ten_enable_go_lint) {
    ten_go_lint("ten_go_binding_lint") {
      # The module path must contain .go files.
      lint_dir = "//core/src/ten_runtime/binding/go/interface/ten_runtime"
      go_exec = "go1.24.3"
    }
  }

  ten_package("ten_go_binding_system_package") {
    package_kind = "system"
    package_output_root_dir_name = "ten_runtime_go"

    resources = [ "manifest.json" ]

    interface_files = exec_script(
            "//.gnfiles/build/scripts/glob_file.py",
            [
              "--dir",
              rebase_path("//core/src/ten_runtime/binding/go/interface/**/*"),
              "--dir-base",
              rebase_path("//core/src/ten_runtime/binding/go/interface/"),
              "--recursive",
              "--only-output-file",
            ],
            "json")

    foreach(interface_file, interface_files) {
      interface_file_rel_path = interface_file.relative_path
      resources += [ "interface/${interface_file_rel_path}=>interface/${interface_file_rel_path}" ]
    }

    tools_files = exec_script(
            "//.gnfiles/build/scripts/glob_file.py",
            [
              "--dir",
              rebase_path("//core/src/ten_runtime/binding/go/tools/**/*"),
              "--dir-base",
              rebase_path("//core/src/ten_runtime/binding/go/tools/"),
              "--recursive",
              "--only-output-file",
            ],
            "json")

    foreach(tool_file, tools_files) {
      tool_file_rel_path = tool_file.relative_path
      resources +=
          [ "tools/${tool_file_rel_path}=>tools/${tool_file_rel_path}" ]
    }

    # ten_runtime_go is now a static library and is output to target_out_dir
    if (ten_force_mingw_for_go_binding && is_win && !is_mingw) {
      # MinGW toolchain output
      _mingw_toolchain = "//.gnfiles/build/toolchain/mingw:mingw"
      ten_runtime_go_target_out_dir = get_label_info(
          "native:ten_runtime_go_mingw(${_mingw_toolchain})",
          "target_out_dir")
      ten_runtime_go_lib_path =
          "${ten_runtime_go_target_out_dir}/libten_runtime_go.a"

      # Get the MinGW toolchain root_out_dir for shared libraries
      # MinGW shared libraries are output to root_out_dir, not target_out_dir
      _mingw_root_out_dir = get_label_info(
          "//core/src/ten_runtime:ten_runtime_shared_for_go(${_mingw_toolchain})",
          "root_out_dir")

      # MinGW-compiled shared libraries and their import libraries
      ten_runtime_mingw_dll = "${_mingw_root_out_dir}/ten_runtime.dll"
      ten_runtime_mingw_implib = "${_mingw_root_out_dir}/libten_runtime.dll.a"
      ten_utils_mingw_dll = "${_mingw_root_out_dir}/ten_utils.dll"
      ten_utils_mingw_implib = "${_mingw_root_out_dir}/libten_utils.dll.a"
    } else {
      ten_runtime_go_target_out_dir = get_label_info("native:ten_runtime_go", "target_out_dir")
      if (is_win && !is_mingw) {
        # MSVC static library uses .lib extension without lib prefix
        ten_runtime_go_lib_path = "${ten_runtime_go_target_out_dir}/ten_runtime_go.lib"
      } else {
        # MinGW/Linux/macOS static library uses .a extension with lib prefix
        ten_runtime_go_lib_path = "${ten_runtime_go_target_out_dir}/libten_runtime_go.a"
      }
    }

    # Build ten_runtime_go_output_libs for use in resources
    ten_runtime_go_output_libs = [ ten_runtime_go_lib_path ]

    # When using MinGW for Go binding, also include the MinGW-compiled
    # shared libraries and their import libraries
    if (ten_force_mingw_for_go_binding && is_win && !is_mingw) {
      ten_runtime_go_output_libs += [
        ten_runtime_mingw_dll,
        ten_runtime_mingw_implib,
        ten_utils_mingw_dll,
        ten_utils_mingw_implib,
      ]
    }

    foreach(lib, ten_runtime_go_output_libs) {
      libname = get_path_info(rebase_path(lib), "file")
      resources += [ "${lib}=>lib/${libname}" ]
    }

    deps = [ "native:ten_runtime_go" ]

    # Add explicit dependency on MinGW targets when forcing MinGW
    if (ten_force_mingw_for_go_binding && is_win && !is_mingw) {
      deps += [
        "native:ten_runtime_go_mingw(${_mingw_toolchain})",
        "//core/src/ten_runtime:ten_runtime_shared_for_go(${_mingw_toolchain})",
        "//core/src/ten_utils:ten_utils_shared_for_go(${_mingw_toolchain})",
      ]
    }

    if (ten_enable_go_lint) {
      deps += [ ":ten_go_binding_lint" ]
    }
  }

  if (ten_enable_ten_manager) {
    ten_package_publish("upload_ten_go_binding_system_package_to_server") {
      base_dir =
          rebase_path("${root_out_dir}/ten_packages/system/ten_runtime_go")
      deps = [
        ":ten_go_binding_system_package",
        "native:ten_runtime_go",
      ]
    }
  }
}

group("go") {
  deps = []

  if (ten_enable_go_binding) {
    deps += [ "native:ten_runtime_go" ]

    if (ten_enable_ten_manager) {
      deps += [ ":upload_ten_go_binding_system_package_to_server" ]
    }
  }
}
